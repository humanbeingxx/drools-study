package priv.cxs.drools.usetest.official.politician;
dialect  "mvel"

rule "PoliticianRule : We have an honest Politician"
    salience 20
    when
        exists( Politician( honest == true ) )
    then
        insertLogical( new Hope() );
end


rule "PoliticianRule : Hope Lives"
    salience 10
        when
            exists( Hope() )
        then
            System.out.println("Hurrah!!! Democracy Lives");
end


rule "PoliticianRule : Corrupt the Honest"
    when
        politician : Politician( honest == true )
        exists( Hope() )
    then
        System.out.println( "I'm an evil corporation and I have corrupted " + politician.getName() );
        modify ( politician ) { honest = false };
end


rule "PoliticianRule : Hope is Dead"
    when
        not( Hope() )
    then
        System.out.println( "We are all Doomed!!! Democracy is Dead" );
end